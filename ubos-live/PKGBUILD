developer=http://indiecomputing.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.38
pkgrel=1
pkgdesc="Device-side functionality of UBOS Live"
arch=('any')
license=("PPL3")
options=('!strip')
depends=(
    'coreutils'
    'ubos-admin'
    'perl-lwp-protocol-https'
)
_vendor_perl=/usr/share/perl5/vendor_perl
install=install

package() {
# Systemd
    install -D -m644 ${startdir}/systemd/* -t ${pkgdir}/usr/lib/systemd/system/

# Initialize on boot with staff
    install -D -m644 ${startdir}/etc/ubos/staff-boot-callbacks/* -t ${pkgdir}/etc/ubos/staff-boot-callbacks/

# Code
    install -D -m755 ${startdir}/vendor_perl/UBOS/Commands/*.pm            -t ${pkgdir}${_vendor_perl}/UBOS/Commands/
    install -D -m755 ${startdir}/vendor_perl/UBOS/Live/*.pm                -t ${pkgdir}${_vendor_perl}/UBOS/Live/
    install -D -m755 ${startdir}/vendor_perl/UBOS/Live/StaffCallbacks/*.pm -t ${pkgdir}${_vendor_perl}/UBOS/Live/StaffCallbacks/
    install -D -m755 ${startdir}/bin/*                                     -t ${pkgdir}/usr/share/${pkgname}/bin/

# Insert inlined images
    perl -p -i \
        -e "s!UBOS_STAFF_IMAGE_BASE64!$(base64 ${startdir}/images/ubos-staff.png | tr -d '\n')!" \
        ${pkgdir}${_vendor_perl}/UBOS/Live/UbosLiveHtmlConstants.pm
    perl -p -i \
        -e "s!HELP_IMAGE_BASE64!$(base64 ${startdir}/images/help.png | tr -d '\n')!" \
        ${pkgdir}${_vendor_perl}/UBOS/Live/UbosLiveHtmlConstants.pm
    perl -p -i \
        -e "s!COPY_IMAGE_BASE64!$(base64 ${startdir}/images/copy.png | tr -d '\n')!" \
        ${pkgdir}${_vendor_perl}/UBOS/Live/UbosLiveHtmlConstants.pm
}
