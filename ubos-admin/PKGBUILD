developer="http://indiecomputing.com/"
url="http://ubos.net/"
maintainer=${developer}
pkgname=$(basename $(pwd))
pkgver=0.417
pkgrel=1
pkgdesc="UBOS infrastructure and tools"
arch=('any')
license=('PPL3')
depends=(
    'apache'
    'avahi'
    'btrfs-progs'
    'cronie'
    'dosfstools'
    'iptables'
    'java-runtime-common'
    'nss-mdns'
    'perl-archive-zip'
    'perl-cgi'
    'perl-dbi'
    'perl-libwww'
    'perl-net-ip'
    'perl-uri'
    'php-apache'
    'php-intl'
    'snapper'
    'sudo'
    'ubos-keyring'
    'ubos-perl-utils' )
backup=(
    'etc/ubos/config.json'
    'etc/diet4j/diet4j.conf'
)
_vendor_perl=/usr/share/perl5/vendor_perl

options=('!strip')
install=install

package() {
# Code
    install -D -m755 ${startdir}/bin/ubos-admin -t ${pkgdir}/usr/bin/

    for d in AppConfigurationItems Backup BackupOperations Commands Databases DataTransferProtocols HostnameCallbacks \
             Networking Networking/NetConfigs Roles StaffCallbacks TemplateProcessor; do
        for f in ${startdir}/vendor_perl/UBOS/${d}/*.pm; do
            if [ -r "$f" ]; then
                install -D -m755 ${f} -t ${pkgdir}${_vendor_perl}/UBOS/${d}/
            fi
        done
    done
    for f in ${startdir}/vendor_perl/UBOS/*.pm; do
        install -D -m755 ${f} -t ${pkgdir}${_vendor_perl}/UBOS/
    done

    install -D -m755 ${startdir}/bin/* -t ${pkgdir}/usr/share/${pkgname}/bin/

# Data directory / mount point
    mkdir -p -m755 ${pkgdir}/ubos

# Config files
    install -D -m644 ${startdir}/etc/ubos/config.json -t ${pkgdir}/etc/ubos/

# Web server content files
    install -D -m644 ${startdir}/www/_common/css/*.css    -t ${pkgdir}/srv/http/_common/css/
    install -D -m644 ${startdir}/www/_common/images/*.png -t ${pkgdir}/srv/http/_common/images/
    install -D -m644 ${startdir}/www/_errors/*.html       -t ${pkgdir}/srv/http/_errors/

    install -D -m644 ${startdir}/www/_appicons/default/{72x72,144x144}.png -t ${pkgdir}/srv/http/_appicons/default/

    for d in maintenance nosuchsite; do
        for f in ${startdir}/www/placeholders/${d}/*.html; do
            install -D -m644 ${f} -t ${pkgdir}/srv/http/placeholders/${d}/
        done
        ln -s /srv/http/_errors ${pkgdir}/srv/http/placeholders/$d/_errors
        ln -s /srv/http/_common ${pkgdir}/srv/http/placeholders/$d/_common
    done

# CGI files
    install -D -m755 ${startdir}/cgi-bin/*.pl -t ${pkgdir}/usr/share/ubos/cgi-bin/

# Systemd -- note that the ipXtables go into /etc not /usr/lib so they don't conflict with iptables' version
    install -D -m644 ${startdir}/systemd/{certbot,smtp-server@,ubos-admin,ubos-ready,ubos-reboot,ubos-shutdown}.service -t ${pkgdir}/usr/lib/systemd/system/
    install -D -m644 ${startdir}/systemd/certbot.timer                -t ${pkgdir}/usr/lib/systemd/system/
    install -D -m644 ${startdir}/systemd/{iptables,ip6tables}.service -t ${pkgdir}/etc/systemd/system/
    install -D -m644 ${startdir}/systemd/tun9x.network                -t ${pkgdir}/usr/lib/systemd/network/

# Snapper
    install -D -m644 ${startdir}/etc/snapper/config-templates/ubos-default -t ${pkgdir}/etc/snapper/config-templates/

# Avahi
# Building Avahi ourselves does not currently work on armv6; no idea why. So we use the prebuilt
# packages and add what we need from here
    install -D -m644 ${startdir}/avahi/*.service -t ${pkgdir}/etc/avahi/services/

# Repositories
    mkdir -p ${pkgdir}/etc/pacman.d/repositories.d/

# Other config files
    install -D -m644 ${startdir}/etc/php/conf.d/*.ini    -t ${pkgdir}/etc/php/conf.d/
    install -D -m644 ${startdir}/etc/ubos/open-ports.d/* -t ${pkgdir}/etc/ubos/open-ports.d/
    install -D -m644 ${startdir}/etc/diet4j/diet4j.conf  -t ${pkgdir}/etc/diet4j/
    mkdir -p ${pkgdir}/etc/ubos/{hostname,state}-callbacks
    install -D -m644 ${startdir}/etc/ubos/staff-boot-callbacks/* -t ${pkgdir}/etc/ubos/staff-boot-callbacks/
    install -D -m644 ${startdir}/etc/logrotate.d/*               -t ${pkgdir}/etc/logrotate.d/

# Key refresh touch file
    touch ${pkgdir}/etc/ubos/key-refresh.touch

# Give cronie no reason to complain
    install -D -m644 /dev/null ${pkgdir}/etc/crontab

# Keep old directory, so after-boot file can be written prior to upgrade
    mkdir -p ${pkgdir}/var/lib/ubos

# A directory to mount a UBOS staff to in a container
    mkdir -p -m755 ${pkgdir}/UBOS-STAFF
}
