developer="http://indiecomputing.com/"
url="http://ubos.net/"
maintainer=$developer
pkgname=$(basename $(pwd))
pkgver=0.155
pkgrel=2
pkgdesc="UBOS infrastructure and tools"
arch=('any')
license=('GPL')
depends=(
    'ubos-perl-utils'
    'ubos-keyring'
    'cronie'
    'ntp'
    'btrfs-progs'
    'sudo'
    'perl-cgi'
    'perl-dbi'
    'perl-archive-zip'
    'apache'
    'mod_wsgi2' )
backup=(
    'etc/ubos/config.json'
    'etc/httpd/ubos/defaults.conf'
    'etc/httpd/ubos/defaultsites/fallback.conf'
    'etc/httpd/ubos/errors.conf'
    'etc/httpd/ubos/logging.conf'
    'etc/httpd/ubos/usersettings.conf'
)
options=('!strip')
install=install

package() {
# Code
    mkdir -p $pkgdir/usr/bin
    install -m755 $startdir/bin/ubos-admin   $pkgdir/usr/bin/

    for d in AppConfigurationItems Backup Commands Databases Roles TemplateProcessor; do
        mkdir -p $pkgdir/usr/lib/perl5/vendor_perl/UBOS/$d
        for f in $startdir/vendor_perl/UBOS/$d/*.pm; do
            install -m755 $f $pkgdir/usr/lib/perl5/vendor_perl/UBOS/$d/
        done
    done
    for f in $startdir/vendor_perl/UBOS/*.pm; do
        install -m755 $f $pkgdir/usr/lib/perl5/vendor_perl/UBOS/
    done

    mkdir -p $pkgdir/usr/share/$pkgname/bin
    install -m755 $startdir/bin/{ubos-admin-initialize,ubos-mysqld-start-pre,ubos-apachectl,ubos-service-prestart,ubos-service-poststop} $pkgdir/usr/share/$pkgname/bin/

# Config files
    mkdir -p $pkgdir/etc/ubos/ssl
    install -m644 $startdir/etc/ubos/config.json $pkgdir/etc/ubos/

# Site files and AppConfiguration parameter files
    mkdir -p -m755 $pkgdir/var/lib/ubos/sites
    mkdir -p -m700 $pkgdir/var/lib/ubos/appconfigpars

# Manifest files
    mkdir -p $pkgdir/var/lib/ubos/manifests

# Backup files
    mkdir $pkgdir/var/lib/ubos/backups
    mkdir -m700 $pkgdir/var/lib/ubos/backups/update

# Resource management
    mkdir -p $pkgdir/var/lib/ubos/resources

# Web server config files
    mkdir -p $pkgdir/etc/httpd/conf
    mkdir -p $pkgdir/etc/httpd/ubos/{appconfigs,defaultsites,mods-available,mods-enabled,sites,ssl}
    install -m644 $startdir/etc/httpd/conf/httpd-ubos.conf $pkgdir/etc/httpd/conf/
    for f in $startdir/etc/httpd/ubos/*.conf; do
        install -m644 $f $pkgdir/etc/httpd/ubos/
    done
    for f in $startdir/etc/httpd/ubos/defaultsites/*; do
        install -m644 $f $pkgdir/etc/httpd/ubos/defaultsites/
    done
    for f in $startdir/etc/httpd/ubos/mods-available/*.load; do
        install -m644 $f $pkgdir/etc/httpd/ubos/mods-available/
    done

# Web server content files
    mkdir -p $pkgdir/srv/http/_common/{css,images}
    install -m644 $startdir/www/_common/css/*.css $pkgdir/srv/http/_common/css/
    install -m644 $startdir/www/_common/images/*.png $pkgdir/srv/http/_common/images/

    mkdir -p $pkgdir/srv/http/_appicons/default
    install -m644 $startdir/www/_appicons/default/{72x72,144x144}.png $pkgdir/srv/http/_appicons/default/

    mkdir -p $pkgdir/srv/http/placeholders/maintenance
    mkdir -p $pkgdir/srv/http/placeholders/nosuchsite

    for d in maintenance nosuchsite; do
        for f in $startdir/www/placeholders/$d/*.html; do
            install -m644 $f $pkgdir/srv/http/placeholders/$d/
        done
    done

    mkdir -p $pkgdir/srv/http/sites
    mkdir -p $pkgdir/srv/http/wellknown

# CGI files
    mkdir -p $pkgdir/usr/share/ubos/cgi-bin
    install -m755 $startdir/cgi-bin/{show-apps,render-appicon}.pl $pkgdir/usr/share/ubos/cgi-bin/

# Tomcat
    mkdir -p -m 775 $pkgdir/etc/tomcat8
    mkdir -p $pkgdir/etc/tomcat8/ubos/sites-apps
    install -m644 $startdir/etc/tomcat8/server-ubos.xml.tmpl $pkgdir/etc/tomcat8/
    mkdir -p -m 775 $pkgdir/var/lib/tomcat8
    mkdir -p $pkgdir/var/lib/tomcat8/sites

# Mysql
    # Our mysql-ubos.service runs the mysql initialization as user mysql:mysql, and thus cannot
    # create /var/lib/mysql
    mkdir -p -m 700 $pkgdir/var/lib/mysql
    chown 89:1 $pkgdir/var/lib/mysql
    mkdir -p -m 755 $pkgdir/etc/mysql
    install -m644 $startdir/etc/mysql/mysql-ubos.cnf $pkgdir/etc/mysql/

# Systemd
    mkdir -p -m 755 $pkgdir/usr/lib/systemd/system/
    install -m 644 $startdir/systemd/ubos-*.service $pkgdir/usr/lib/systemd/system/

# Repositories
    mkdir -p $pkgdir/etc/pacman.d/repositories.d/

# Other config files
    mkdir -p $pkgdir/etc/php/conf.d
    install -m644 $startdir/etc/php/conf.d/*.ini $pkgdir/etc/php/conf.d/

# A directory to mount a UBOS staff to in a container
    mkdir -p $pkgdir/UBOS-STAFF
}
